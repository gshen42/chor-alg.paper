\section{A Minimal Algebraic Effects Framework in Agda}
\label{sec:alg-eff-agda}

In this section, we define a minimal algebraic effects framework in Agda, which we will use to implement CP in the next section.
%
No prior knowledge of algebraic effects is assumed.
%
We introduce each concept first from a mathematical perspective and then relate it to programming.
%
Due to lack of space, we do not include any examples in this section, but the next section can be seen as a demonstration of the framework.
%
Our presentation is influenced by \citet{bauer-2019} and \citet{kidney-2023}, and we refer the reader to them for a comprehensive introduction to algebraic effects.

\begin{code}[hide]
open import Data.Product using (Σ; Σ-syntax; _,_)
open import Effect.Applicative using (RawApplicative)
open import Effect.Functor using (RawFunctor)
open import Effect.Monad using (RawMonad)
open import Function using (_∘_; _∘′_; _$′_)
open import Level using (Level)

infix 21 ⟦_⟧
\end{code}

\begin{code}[hide]
-- the standard library `mkRawMonad` is not general enough for our use
-- so we define our own version
module _ where

  private
    variable
      f g : Level

  open RawFunctor
  open RawApplicative
  open RawMonad

  mkRawApplicative :
    (F : Set f → Set g) →
    (pure : ∀ {A} → A → F A) →
    (app : ∀ {A B} → F (A → B) → F A → F B) →
    RawApplicative F
  mkRawApplicative F pure app .rawFunctor ._<$>_ = app ∘′ pure
  mkRawApplicative F pure app .pure = pure
  mkRawApplicative F pure app ._<*>_ = app

  mkRawMonad :
    (F : Set f → Set g) →
    (pure : ∀ {A} → A → F A) →
    (bind : ∀ {A B} → F A → (A → F B) → F B) →
    RawMonad F
  mkRawMonad F pure _>>=_ .rawApplicative =
    mkRawApplicative _ pure $′ λ mf mx → do
      f ← mf
      x ← mx
      pure (f x)
  mkRawMonad F pure _>>=_ ._>>=_ = _>>=_
\end{code}

\subsection{Signatures and Algebras}

A signature \AgdaRecord{Sig} specifies the equipped operations of an algebra, which includes a type \AgdaField{Op} of operations and a function \AgdaField{Arity} giving the number of arguments (represented as the cardinality of a type) of each operation:\footnote{Agda uses an infinite hierarchy of universes where $\AgdaPrimitive{Set} : \AgdaPrimitive{Set}_1 : ... : \AgdaPrimitive{Set}_n : \AgdaPrimitive{Set}_{n+1}$ to avoid paradoxes.
%
For ease of presentation in this paper, we overconstrain the universe of \AgdaField{Op} to be \AgdaPrimitive{Set₁} (similarily for \AgdaField{Arity}).
%
The actual implementation is universe-polymorphic,
but readers can safely ignore the universe hierarchy without missing the key point of the paper.}
%
\begin{center}\begin{code}
record Sig : Set₂ where
  constructor _◁_
  field
    Op : Set₁
    Arity : Op → Set
\end{code}\end{center}
%
\begin{code}[hide]
open Sig
\end{code}
%
A set $X$ that implements the operations of a signature $\mathbb{F}$ is called an $\mathbb{F}$-algebra.
%
An $\mathbb{F}$-algebra on the carrier set $X$, written as $\mathbb{F}$ \AgdaFunction{-Alg[} $X$ \AgdaFunction{]} in Agda, is a function of type \AgdaFunction{⟦} 𝔽 \AgdaFunction{⟧} $X \rightarrow X$:
%
\[
\begin{minipage}{.5\textwidth}
\begin{center}\begin{code}
⟦_⟧ : Sig → Set₁ → Set₁
⟦ Op ◁ Ar ⟧ X = Σ[ o ∈ Op ] (Ar o → X)
\end{code}\end{center}
\end{minipage}
%
\begin{minipage}{.5\textwidth}
\begin{center}\begin{code}
_-Alg[_] : Sig → Set₁ → Set₁
𝔽 -Alg[ X ] = ⟦ 𝔽 ⟧ X → X
\end{code}\end{center}
\end{minipage}
\]
%
\AgdaFunction{⟦} 𝔽 \AgdaFunction{⟧} $X$ denotes an operation paired with its arity number of elements from the carrier set --- a fully applied operation.
%
\AgdaFunction{⟦} 𝔽 \AgdaFunction{⟧} $X \rightarrow X$ allows us to make a new element out of a fully applied operation, the very nature of an algebra.

In programming, we can view an effectful operation as giving rise to an algebra, with the allowed effects being the signature, the result of an effect being the arity of the operation, and a carrier set of such an algebra being a functional model of the effectful computation.

\subsection{The Free Algebra}

Among all the algebras of a signature $\mathbb{F}$, we are particularly interested in one called the \emph{free algebra}.
%
Rather than performing operations in the carrier set, the free algebra merely records them as a data type, which we call \AgdaDatatype{Term}:
%
\begin{center}\begin{code}
data Term (𝔽 : Sig) (A : Set) : Set₁ where
  var : A → Term 𝔽 A
  op : ⟦ 𝔽 ⟧ (Term 𝔽 A) → Term 𝔽 A
\end{code}\end{center}
%
The \AgdaInductiveConstructor{var} constructor denotes a variable drawn from some set $A$.
%
The \AgdaInductiveConstructor{op} constructor denotes a fully applied operation to some other terms.

In programming, \AgdaDatatype{Term}s correspond to programs where effects are left uninterpreted, with variables being pure computations and operations being effectful computations.
%
\AgdaDatatype{Term}s also form a monad (it is actually the free monad), which allows us to chain them together:
%
\begin{center}\begin{code}
return : ∀ {𝔽} {A} → A → Term 𝔽 A
return = var

_>>=_ : ∀ {𝔽} {A B} → Term 𝔽 A → (A → Term 𝔽 B) → Term 𝔽 B
var x       >>= f =  f x
op (o , k)  >>= f =  op (o , _>>= f ∘ k)
\end{code}\end{center}
%
We also provide a helper function \AgdaFunction{perform}, which constructs a \AgdaDatatype{Term} that performs an operation and immediately returns its result:
%
\begin{center}\begin{code}
perform : ∀ {𝔽} (o : Op 𝔽) → Term 𝔽 (Arity 𝔽 o)
perform o = op (o , var)
\end{code}\end{center}

\begin{code}[hide]
instance
  term-monad : ∀ {𝔽} → RawMonad (Term 𝔽)
  term-monad = mkRawMonad _ return _>>=_
\end{code}

\subsection{Effect Handlers}

One of the reasons why \AgdaDatatype{Term}s are called the free algebra of $\mathbb{F}$ is that they are freely interpretable.
%
Given another $\mathbb{F}$-algebra $X$ and a substitution of variables from $X$, we can interpret a term as an element of $X$:
%
\begin{center}\begin{code}
interp : ∀ {𝔽} {X A} → 𝔽 -Alg[ X ] → (A → X) → Term 𝔽 A → X
interp alg f (var x)       = f x
interp alg f (op (o , k))  = alg (o , interp alg f ∘ k)
\end{code}\end{center}
%
For variables, \AgdaFunction{interp} uses the substitution $f$ to map them to $X$.
%
For operations, \AgdaFunction{interp} first recursively interprets the arguments and then uses ${alg}$ to make a new element of $X$ from a fully applied operation.

In programming, \AgdaFunction{interp} forms the foundation of effect handlers.
%
It lets us systematically handle uninterpreted effects of a computation.
